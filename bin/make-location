#!/usr/bin/perl -w
# -*-perl-*-

use strict;
use lib './lib';

use IO::File;
use Getopt::Long ();
use Peaks::ListsOfJohn;

my @g_options = qw(
                   name=s
		   output-directory=s
		  );
my %g_options = ();

exit(main());

sub main {
  get_options();

#  Scramble::Logger::set_verbose(1);
#  Scramble::Area::open();
#  Scramble::Location::open_all();
#  my @locations = Scramble::Location::get_all()->find(name => $g_options{name});

  my $peaks = $Peaks::ListsOfJohn::Peaks{$g_options{name}};
  if (! defined $peaks) {
    printf("Lists of John has no such peak '%s'", $g_options{name});
    return 1;
  }

  my $filename = $g_options{name};
  $filename =~ s/[^\w]+/-/g;
  $filename = $g_options{'output-directory'} . "/$filename.xml";
  die "$filename already exists" if -e $filename;

  my @peaks = values %$peaks;
  foreach my $i (0 .. $#peaks) {
    my $peak = $peaks[$i];
    print "$i) $peak->{quad}\n";
  }

  my $choice;
  do {
    print "Which? ";
    chomp($choice = <STDIN>);
  } while ($choice !~ /^\d+$/ || $choice < 0 || $choice > $#peaks);

  my $xml = make_location_xml($peaks[$choice]);

  my $fh = IO::File->new($filename, "w");
  $fh->print($xml);
  $fh->close();

  print "Created $filename\n";

  return 0;
}

sub usage {
  my ($prog) = ($0 =~ /([^\/]+)$/);
  print("Usage: $prog [ OPTIONS ]\nOptions:\n\t--"
	. join("\n\t--", @g_options));
}

sub get_options {
  local $SIG{__WARN__};
  if (! Getopt::Long::GetOptions(\%g_options, @g_options)
      || $g_options{'help'})
    {
      print usage();
      exit 1;
    }

  foreach my $required (qw(name output-directory)) {
    die "Missing --$required" unless exists $g_options{$required};
  }
}

sub make_location_xml {
  my ($peak) = @_;

  my $unofficial_name = $peak->{'unofficial-name'} ? 1 : 0;

return <<EOT;
<location
    type="peak"
    elevation="$peak->{elevation}"
    prominence="$peak->{prominence}"
>

<name value="$peak->{name}"
      unofficial-name="$unofficial_name"
/>

<coordinates datum="WGS84"
             latitude="$peak->{coordinates}{latitude}"
	     longitude="$peak->{coordinates}{longitude}"
/>

<areas>
<area id="WA"/>
<area id="$peak->{quad}"/>
</areas>

</location>
EOT
}
